// File name:cpu_ram_rom/CPU.hdl

/**
 * The Hack CPU (Central Processing unit), consisting of an ALU,
 * two registers named A and D, and a program counter named PC.
 * The CPU is designed to fetch and execute instructions written in 
 * the Hack machine language. In particular, functions as follows:
 * Executes the inputted instruction according to the Hack machine 
 * language specification. The D and A in the language specification
 * refer to CPU-resident registers, while M refers to the external
 * memory location addressed by A, i.e. to Memory[A]. The inM input 
 * holds the value of this location. If the current instruction needs 
 * to write a value to M, the value is placed in outM, the address 
 * of the target location is placed in the addressM output, and the 
 * writeM control bit is asserted. (When writeM==0, any value may 
 * appear in outM). The outM and writeM outputs are combinational: 
 * they are affected instantaneously by the execution of the current 
 * instruction. The addressM and pc outputs are clocked: although they 
 * are affected by the execution of the current instruction, they commit 
 * to their new values only in the next time step. If reset==1 then the 
 * CPU jumps to address 0 (i.e. pc is set to 0 in next time step) rather 
 * than to the address resulting from executing the current instruction. 
 */

CHIP CPU {

    IN  inM[16],         // M value input  (M = contents of RAM[A])
        instruction[16], // Instruction for execution
        reset;           // Signals whether to re-start the current
                         // program (reset==1) or continue executing
                         // the current program (reset==0).

    OUT outM[16],        // M value output
        writeM,          // Write to M? 
        addressM[15],    // Address in data memory (of M)
        pc[15];          // address of next instruction

    PARTS:
    
    Mux16 (a=instruction, b=alu-out, sel=instruction[15], out=a-reg-in);
    //Load-of-A-reg = (!instruction[15] || instruction[5]) //d1==instruction[5]
    Not (in=instruction[15], out=neg-instruction-msb);
    Or (a=neg-instruction-msb, b=instruction[5], out=load-a-reg);
    ARegister (in=a-reg-in, load=load-a-reg, out=a-reg-out, out[0..14]=addressM); // A reg
    Mux16 (a=a-reg-out, b=inM, sel=instruction[12], out=alu-y-in);
    And (a=instruction[4], b=instruction[15], out=load-d-reg); // valid only for compute instr (c-intr -> msb is high)
    DRegister (in=alu-out, load=load-d-reg, out=alu-x-in); // D reg
    ALU (x=alu-x-in, y=alu-y-in, zx=instruction[11] , nx=instruction[10] , zy=instruction[9] , ny=instruction[8] , f =instruction[7] , no=instruction[6] , out=alu-out, zr=alu-zero-out, ng=alu-negative-out, out=outM);
    And (a=instruction[3], b=instruction[15], out=writeM);
    Not (in=alu-negative-out, out=alu-out-GoE-0); //alu-out-GoE-0 -> alu-out-Greater-than-or-Equal-to-0
    Not (in=alu-zero-out, out=alu-non-zero-out);
    And (a=alu-out-GoE-0, b=alu-non-zero-out, out=alu-out-GT-0); //alu-out-GT-0 -> alu-out-Greater-Than-0
    Or (a=alu-negative-out, b=alu-zero-out, out=alu-out-LoE-0); //alu-out-LoE-0 -> alu-out-Less-than-or-Equal-to-0
    Mux8Way16 (a=false, b[0]=alu-out-GT-0, b[1..15]=false, c[0]=alu-zero-out, c[1..15]=false, d[0]=alu-out-GoE-0, d[1..15]=false, e[0]=alu-negative-out, e[1..15]=false, f[0]=alu-non-zero-out, f[1..15]=false, g[0]=alu-out-LoE-0, g[1..15]=false, h=true, sel=instruction[0..2], out=not-needed, out[0]=load1 );
    And(a=load1, b=instruction[15], out=load);
    PC (in=a-reg-out, load=load, inc=true, reset=reset, out[0..14]=pc);
}
